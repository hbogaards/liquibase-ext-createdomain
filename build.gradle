buildDir = 'target-gradle'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:2.3.4'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'net.researchgate.release'

group = 'nl.anwb.hv.lib'

sourceCompatibility=1.6
targetCompatibility=1.6

repositories { 
    mavenLocal()
    maven { 
        url "https://nexus.anwb.local/content/groups/public" 
        name 'nexus'
    }
    mavenCentral() 
}

dependencies { 
    compile 'org.liquibase:liquibase-core:2.0.6-SNAPSHOT'
}

jar {
    manifest {
        attributes (provider: 'gradle', 'Liquibase-Package': 'nl.anwb.hv.lib.liquibase.ext')
    }
}

task sourcesJar(type: Jar) {
    group 'Build'
    description 'An archive of the source code'
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

jar.finalizedBy sourcesJar

artifacts {
    sourcesJar
    javadocJar
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

release {
    tagTemplate = '${name}-${version}'

    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
        pushToBranchPrefix = ''
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}
